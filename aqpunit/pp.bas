100 REM POKE and PEEK Unit Tests
110 QU=0:REM 0=Screen,1=Printer
130 GOSUB _init

200 GOSUB _title:ARGS "POKE & PEEK"
210 FOR I=1 TO 3:A=$3000+I:GOSUB _poke:ARGS A,I:NEXT
212 FOR I=1 TO 3:A=$3000+I:GOSUB _peek:ARGS A,I:NEXT I
214 GOSUB _poke_err:ARGS $3008,-1,5
216 GOSUB _poke_err:ARGS $3008,256,5
220 GOSUB _poke_err:ARGS 65536,0,5
222 GOSUB _poke_err:ARGS -65536,0,5
230 GOSUB _peek_err:ARGS -65536,5
232 GOSUB _peek_err:ARGS 65536,5
234 GOSUB _assert_err:ARGS "PEEK(`string`)",13

280 GOTO 300
282 _poke:GETARGS PA,PB:GOSUB _output:ARGS "POKE %%,%%" % (PA,PB):POKE PA,PB:RETURN
284 _peek:GETARGS PA,PB:GOSUB _assert:ARGS "PEEK(%%)=%%" % (PA,PB):RETURN
286 _peek_err:GETARGS PA,PE:GOSUB _assert_err:ARGS "PEEK(%%)" % (PA),PE:RETURN
288 _poke_err:GETARGS PA,PB,QE:QA$="POKE %%,%%" % (PA,PB):ON ERROR GOTO _yes_err:POKE PA,PB:GOTO _no_err

300 GOSUB _title:ARGS "POKEINT & PEEKINT"
310 GOSUB _pokew:ARGS $3008,$0607
312 GOSUB _peek:ARGS $3008,7
314 GOSUB _peek:ARGS $3009,6
316 GOSUB _peekw:ARGS $3008,1543

330 GOSUB _peekw_err:ARGS -65536,5
332 GOSUB _peekw_err:ARGS 65536,5
334 GOSUB _assert_err:ARGS "PEEKINT(`string`)",13

380 GOTO 400
382 _pokew:GETARGS PA,PB:GOSUB _output:ARGS "POKEINT %%,%%" % (PA,PB):POKEINT PA,PB:RETURN
384 _peekw:GETARGS PA,PB:GOSUB _assert:ARGS "PEEKINT(%%)=%%" % (PA,PB):RETURN
386 _peekw_err:GETARGS PA,PE:GOSUB _assert_err:ARGS "PEEKINT(%%)" % (PA),PE:RETURN

400 GOSUB _title:ARGS "POKE @ & PEEK(@"
410 GOSUB _pokep:ARGS 40,$100,69:GOSUB _peekp:ARGS 40,$100,69
412 GOSUB _pokewp:ARGS 40,$101,1776:GOSUB _peekwp:ARGS 40,$101,1776
414 GOSUB _pokewp:ARGS 40,16383,$ABBA:GOSUB _peekwp:ARGS 40,16383,$ABBA
416 GOSUB _peekp:ARGS 40,16383,$BA:GOSUB _peekp:ARGS 41,0,$AB

420 GOSUB _pokep_err:ARGS 0,$1000,1,5:REM Writing to ROM pages
422 GOSUB _pokep_err:ARGS 19,$1000,2,5
424 GOSUB _pokep_err:ARGS 22,$1000,3,5
426 GOSUB _pokep_err:ARGS 31,$1000,4,5
428 GOSUB _pokep_err:ARGS 64,$1000,5,5

430 GOSUB _pokewp_err:ARGS 20,16383,11,6:REM Overflow end of RAM page
432 GOSUB _pokewp_err:ARGS 21,16383,22,6
434 GOSUB _pokewp_err:ARGS 63,16383,33,6
440 GOSUB _peekwp_err:ARGS 20,16383,6
442 GOSUB _peekwp_err:ARGS 21,16383,6
444 GOSUB _peekwp_err:ARGS 63,16383,6

450 GOSUB _pokep_err:ARGS 0,16384,6,5:REM Illegal Address in Page
452 GOSUB _pokewp_err:ARGS 0,16384,44,5
454 GOSUB _peekp_err:ARGS 0,16384,5
456 GOSUB _peekwp_err:ARGS 0,16384,5

470 GOTO 500
472 _pokep:GETARGS PP,PA,PB:GOSUB _output:ARGS "POKE @%%,%%,%%" % (PP,PA,PB):POKE @PP,PA,PB:RETURN
474 _peekp:GETARGS PP,PA,PB:GOSUB _assert:ARGS "PEEK(@%%,%%)=%%" % (PP,PA,PB):RETURN
476 _peekp_err:GETARGS PP,PA,PE:GOSUB _assert_err:ARGS "PEEK(@%%,%%)" % (PP,PA),PE:RETURN
478 _pokep_err:GETARGS PP,PA,PB,QE:QA$="POKE @%%,%%,%%" % (PP,PA,PB):ON ERROR GOTO _yes_err:POKE @PP,PA,PB:GOTO _no_err
482 _pokewp:GETARGS PP,PA,PB:GOSUB _output:ARGS "POKEINT @%%,%%,%%" % (PP,PA,PB):POKEINT @PP,PA,PB:RETURN
484 _peekwp:GETARGS PP,PA,PB:GOSUB _assert:ARGS "PEEKINT(@%%,%%)=%%" % (PP,PA,PB):RETURN
486 _peekwp_err:GETARGS PP,PA,PE:GOSUB _assert_err:ARGS "PEEKINT(@%%,%%)" % (PP,PA),PE:RETURN
488 _pokewp_err:GETARGS PP,PA,PB,QE:QA$="POKEINT @%%,%%,%%" % (PP,PA,PB):ON ERROR GOTO _yes_err:POKEINT @PP,PA,PB:GOTO _no_err

500 REM

